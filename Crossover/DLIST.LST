0 DIM A$(10):DONE=0:PV=-1:PC=0
1 DIM G$(8),M$(15):G$="TEXTGRFX":M$=" 0***123456*7*8"
10 DL=PEEK(560)+256*PEEK(561):? "Display List at $";:A=560:GOSUB 1310:? " (";DL;")"
20 FOR I=0 TO 255:D=DL+I:V=PEEK(D):GOSUB 1000:IF  NOT DONE THEN NEXT I
30 END 
1000 IF PV=V THEN PC=PC+1:RETURN 
1001 PV=V:IF PC THEN ? "         (...x";PC+1;")":PC=0
1005 A=D:GOSUB 1320:? " ";
1010 NH=INT(V/16):NL=V-NH*16:TH=NH
1011 DLI=TH>=8:IF DLI THEN TH=TH-8
1012 LMS=TH>=4:IF LMS THEN TH=TH-4
1013 VSC=TH>=2:IF VSC THEN TH=TH-2
1014 HSC=TH
1020 IF V<100 THEN ? " ";:IF V<10 THEN ? " ";
1021 ? V;" ";
1100 IF NL=0 THEN ? "SKIP";NH+1-(NH>7)*8:RETURN 
1110 IF NL=1 AND LMS THEN ? "JWVB";:DONE=1:GOTO 1201
1111 IF NL=1 THEN ? "JUMP";:GOTO 1201
1200 N=1+4*(NL>8):? G$(N,N+3);M$(NL,NL);
1201 IF DLI THEN ? "+DLI";
1202 IF LMS THEN ? "+LMS(";:GOSUB 1300:? ")";
1203 IF VSC THEN ? "+VSCROL";
1204 IF HSC THEN ? "+HSCROL";
1205 ? :RETURN 
1300 A=D+1:I=I+2
1310 X=PEEK(A+1):GOSUB 1330:X=PEEK(A):GOTO 1330
1320 X=INT(A/256):GOSUB 1330:X=A-X*256
1330 N=INT(X/16):GOSUB 1340:N=X-N*16
1340 C=N+48:IF C>57 THEN C=C+7
1341 ? CHR$(C);:RETURN 
8888 SAVE "D:DLIST.BAS":? "Saved!":END 
