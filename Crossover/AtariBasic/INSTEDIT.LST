; INSTEDIT  3.3
; Copyright 1981
; Sheldon Leemon
; 14400 Elm
; Oak Park, MI 48237
;
; INSTEDIT  3.4
; Thinkyhead 2025

; Initialize far below

0DIMPM$(1),P0$(1),P1$(1),P2$(1),P3$(1),C$(1):G.29000

; Get a key immune from CTRL-3

5T.5:GET#K1,K:T.DOOR:RET.

; Do something clever quickly

10X=USR(ADR("hhhhh"),ADR(C$)+M-K1,ADR(P1$(K23+K2))):P0$(VP)=CUR$:RET.

; Clear Inverse Key, Set CAPS-LOCK. Get a string.

128POK.INVFLG,K0:POK.702,K64:I.F$:RET.

; Get the stick, trigger, key. Handle key (W) WRITE

200S=STICK(K0):T=STRIG(K0):KEY=PEEK(CH):IFKEY=46THENB=110:GOS.K295:G.3000

; OPTION - Memo Pad

207IFPEEK(CONSOL)=K3 THENB=174:GOS.K295:GOS.925

;209POS.0,0:?KEY;"  ";

; (E) EDIT
210IFKEY=42THENB=238:GOS.K295:G.400

; (S) SAVE
215IFKEY=62THENB=94:GOS.K295:G.2000

; (L) LOAD
220IFKEY=K0 THENB=142:GOS.K295:G.2500
225IFFLAG THENGOS.300:G.K200

; TRIGGER Release
230IF(T=K0 ANDT<>OLDT)THENX=PEEK(53260):GOS.350

; (=) Shift DOWN
235IFKEY=K15 THENPOK.CH,K255:TEMP$=C$(M+K7,M+K7):TEMP$(K2)=C$(M,M+K6):C$(M,M+K7)=TEMP$:GOS.K10

; (-) Shift UP
240IFKEY=K14 THENPOK.CH,K255:TEMP$=C$(M+K1,M+K7):TEMP$(K8)=C$(M,M):C$(M,M+K7)=TEMP$:GOS.K10

; (*) Shift RIGHT
245IFKEY=K7 THENPOK.CH,K255:X=USR(ADR("hhhJ"),ADR(C$)+M-K1):GOS.K10

; Check OPTION again
247IFPEEK(CONSOL)=K3 THENB=174:GOS.K295:GOS.925

; (+) Shift LEFT
250IFKEY=K6 THENPOK.CH,K255:X=USR(ADR("hhh"),ADR(C$)+M-K1):GOS.K10

; (T) TWIST
252IFKEY=45THENPOK.CH,K255:X=USR(ADR("hhhJ>"),ADR(C$)+M-K2):GOS.K10:G.K200

; (M) MIRROR
254IFKEY=37THENPOK.CH,K255:X=USR(ADR("hhhJ."),ADR(C$)+M-K2):GOS.K10:G.K200

; (B) BLANK
255IFKEY=(K20+K1)THENPOK.CH,K255:P1$=PM$:C$(M,M+K7)=PM$

; (A) ATARI
259IFKEY=(K64-K1)THENF.I=K0 TOK7:L=M+I+57343:N=PEEK(L):C$(M+I,M+I)=CHR$(N):N.I:GOS.K10:POK.CH,K255

; (I) INVERT
260IFKEY=13THENPOK.CH,K255:F.I=K0 TOK7:L=M+I:N=ASC(C$(L,L)):N=K255-N:C$(L,L)=CHR$(N):N.I:GOS.K10

; (C) COPY
265IFKEY=(K16+K2)THENB=62:GOS.K295:GOS.450

; (R) RESTORE
270IFKEY=K40 THENPOK.CH,K255:P1$(K23+K2)=PM$(K295):F.I=K1 TOK1+K28 STEPK4:C$(M+I/K4,M+I/K4)=P1$(Q24+I):N.I

; (P) PICK
270IFKEY=K10 THENPOK.CH,K255:P1$(K23+K2)=PM$(K295):F.I=K1 TOK1+K28 STEPK4:C$(M+I/K4,M+I/K4)=P1$(Q24+I):N.I

; STICK center?
275IFS=K15 THENOLDT=T:POK.53278,K0:G.K200

; STICK Horizontal
280HP=HP+K4*(S>K4 ANDS<K8)-K4*(S>K8 ANDS<K12):HP=HP+K32*(HP<110)-K32*(HP>138)

; STICK Vertical
281VP=VP-K4*(S=K6 ORS=K14 ORS=K10)+K4*(S=K5 ORS=9ORS=13):VP=VP+K32*(VP<17)-K32*(VP>Q49)
285POK.53248,HP:P0$=PM$:P0$(VP)=CUR$:SO.K0,K200-HP+K2*VP,K10,K8:POK.77,K0
290F.I=K1 TOK4:N.I:SO.K0,K0,K0,K0:F.I=K1 TOK11:N.I:OLDT=T:POK.53278,K0:G.K200

295P0$=PM$:P1$=P0$:POK.CH,K255:GOS.L440-K5:GOS.L440:RET.

300POK.PCOLR3,Q156:POK.Q82,K16:POK.Q83,Q24:X=ASC(P$(K1)):P$=P$(K2):P$(LEN(P$)+K1)=CHR$(X):POS.K16,K3:?P$(K1,K8):GOS.430:RET.

; Low or High bit being set?

350LO=X=K10:HI=X=K8
355F.I=165*HI+80*LO TO165*LO+80*HI STEPK5*LO-K5*HI:SO.K0,I,K10,K8:N.I
360SO.K0,K0,K0,K0:B=(138-HP)/K4:B=INT(K2^B):L=(VP-K20)/K4:L=L+M
370N=ASC(C$(L))+B*HI-B*LO:C$(L,L)=CHR$(N):GOS.K10:RET.

; (E) EDIT

400?" WHICH   CHAR-   ACTER   TO      EDIT?";:GOS.L440+K32
410GOS.L440:POK.PCOLR3,K0:GOS.L445:M=IN*K8+K1:GOS.K10:GOS.800:FLAG=K0:PM$(K295)=P1$(K23+K2)
420POS.K27,K1:?"";C;"":POS.K34+K1,K1:?CHR$(162);"";CHR$(C);CHR$(162);:G.K200

; Perform a short delay

425F.I=K1 TOK200+K128:N.I

; Perform a shorter delay

430F.I=K1 TOK48:N.I:RET.

; Set LMARGN,RMARGN to 16,23, move to 16,2, PCOLR3 = B

435POK.Q82,K16:POK.Q83,K23:POS.K16,K2:POK.PCOLR3,B:RET.

; Clear the area bound by LMARGN,RMARGN
440POS.K16,K2:?"                                                                  ":POS.K16,K2:RET.

; Restore LMARGN,RMARGN to 2,39
445POK.Q82,K2:POK.Q83,K34+K5:RET.

; (C) COPY
450?" WHICH   CHAR-   ACTER   TO      COPY?";:GOS.L440+K32:GOS.L440:POK.PCOLR3,K0
460GOS.L445:X=IN*K8+K1:C$(M,M+K7)=C$(X,X+K7):POK.Q82,K2:GOS.K10:RET.

; C = the char, IN = the screen code

472GOS.K5:C=K-K128*(K>=K128):?"";CHR$(C):IN=PEEK(DL+294):RET.

800F.I=K0 TOK2:B=K40*I:F.J=K0 TOK2:POK.DL+380+B+J,IN:POK.DL+386+B+J,IN+K128:N.J:N.I
820POK.DL+384,IN:POK.DL+464,IN+K128:IFHF=K0 THENHEART$=C$(Q513,Q520)
825IFGF=K1 THENHF=K0:RET.
826IFIN>=K64 THENPOK.A1791,BASE+K2:IN1=IN-K64:IN2=IN+K64:IN3=IN2+K64:IFIN=K64 THENC$(Q513,Q520)=HEART$:HF=K0
830IFIN>K64 THENC$(Q513,Q520)=PM$:HF=K1
835IFIN<K64 THENPOK.A1791,BASE:C$(Q513,Q520)=HEART$:IN1=IN+K64:IN2=IN1+K64:IN3=IN2+K64:HF=K0
840IFGF=K2 THENRET.

; Draw characters in the bottom area

845F.I=DL+716 TODL+718:POK.I,IN:POK.I+K28+K1,IN:N.I:POK.DL+724,IN:POK.DL+739,IN
850I=DL+770:POK.I,IN:POK.I+K1,IN:POK.I+K2,IN+K128:POK.I+K3,IN+K128:POK.I-K3,K128+IN:POK.I+K6,IN
860F.I=DL+793 TODL+795:POK.I,IN:POK.I+K14,IN3:N.I:POK.DL+797,IN2:POK.DL+805,IN1
870I=DL+830:POK.I,IN+K128:POK.I+K1,IN+K128:POK.I+K2,IN:POK.I+K3,IN:POK.I-K3,IN:POK.I+K6,IN+K128
880F.I=DL+853 TODL+855:POK.I,IN1:POK.I+K14,IN2:N.I:POK.DL+857,IN3:POK.DL+865,IN:RET.

; OPTION - Memo Pad

925?"MEMO PAD        0-2) GR 3-5) IR 6) KEEP 7) EXIT"
930POK.INVFLG,K0:GOS.K5:G=K:IFG<K48 ORG>55THEN930
935GOS.L440:POK.PCOLR3,K0:GOS.L445:G=G-K48:IFG=K6 THEN945
937IFG=K7 THENPOK.SDMCTL,K0:LAST=K20+K1:G=K2:GOS.990:POK.CONSOL,K0:GF=K0:G.31310
938GF=K1:IFG<K3 THENG=G+K2:IFG>K2 THENG=G+K3:GF=K2
940LAST=K20+K8*(G=K2 ORG=K4 ORG=K6)+K2*(G=K5 ORG=K7)+K5*(G=K3):GOS.990
945POK.CONSOL,K0:POK.CRSINH,K0:POS.39,K11:?" ";:POK.CH,K255:G.L965

; Get keys and draw them in the memo area

950GOS.K5:B=K:IFB=125ORB=Q156 THEN950
955X=PEEK(Q82+K2):IFX<K12 OR(B=K28 ANDX=K12)ORX>LAST-K6 THENPOK.Q82+K2,K12:G.L965
960?CHR$(B);:POK.CH,K255
965IFPEEK(CONSOL)=K3 THEN980
970IFPEEK(CH)=K255 THENG.L965
975G.950

; Exit Memo Pad. Inhibit the cursor.

980POK.CRSINH,K1:B=126:GOS.L440-K5:?"RETURN  TO EDIT MODE":GOS.L425:GOS.L440:GOS.L445:FLAG=K1:RET.
990F.I=17TOLAST:POK.DL+I,G:N.I:POK.DL+LAST+K1,65:POK.DL+LAST+K2,PEEK(SDLSTL):POK.DL+LAST+K3,PEEK(SDLSTH)
995POS.K2,K12:?"":RET.

; (W) WRITE

2000IFHF THENC$(Q513,Q520)=HEART$:HF=K0
2005T.2005:CL.#K3:GOS.L440:IFDISK=K0 THEN2300
2010?" WHICH   NAME   TO SAVE";:GOS.K128:IFF$=""THEN?" CANCEL  SAVE";:G.2270
2100TEMP$="D:":TEMP$(K3)=F$:TEMP$(LEN(TEMP$)+K1)=".SET":O.#K3,K8,K0,TEMP$
2230POK.884,K0:POK.885,BASE:POK.888,K0:POK.889,K4:POK.882,K11:X=USR(ADR("hhhLV"),K48)
2240CL.#K3:GOS.L440:?" SAVE    DONE"
2270POK.NMIEN,Q192:GOS.L425:T.DOOR:CL.#K3:FLAG=K1:GOS.L440:GOS.L445:POK.CH,K255:G.K200

; No Disk Ready
2300?HR$:GOS.K5:C=K:T.2005:IFC=K27 THEN?:?" CANCEL  SAVE";:G.2270
2310GOS.L440:O.#K3,9,K128,"C:":G.2230

; (L) LOAD

2500T.2500:CL.#K3:CL.#K5:GOS.L440:DF=K1:IFDISK=K0 THEN2650
2520?" WHICH   SET    TO LOAD";
2540F$=CHR$(Q155):POS.K23,K4:GOS.K128:IFF$="*"THEN?" ":GOS.2700:G.2540
2560IFF$=""THEN?" CANCEL":?" LOAD":G.2630
2570TEMP$="D:":TEMP$(K3)=F$:TEMP$(LEN(TEMP$)+K1)=".SET":O.#K3,K4,K0,TEMP$:POK.756,BASE
2600POK.884,K0:POK.885,BASE:POK.888,K0:POK.889,K4:POK.882,K7:X=USR(ADR("hhhLV"),K48)
2620CL.#K3:GOS.L440:?" LOAD    DONE";:HEART$=C$(Q513,Q520):POK.756,224
2630POK.NMIEN,Q192:GOS.L425:T.DOOR:CL.#K3:CL.#K5:FLAG=K1:GOS.L440:GOS.L445:POK.CH,K255:GOS.32000:G.K200

; No Disk Ready
2650?HR$:GOS.K5:C=K:T.2500:IFC=K27 THEN?" CANCEL  LOAD":G.2630
2670GOS.L440:POK.756,BASE:O.#K3,K5,K128,"C:":G.2600

; Get list of *.SET files
2700IFDF THENO.#K5,6,K0,"D:*.SET":DF=K0
2705F.I=K1 TOK10:I.#K5;F$:IFF$(K3)<"A"THENPOP:DF=K1:CL.#K5:G.2730
2710IFINT(I/K2)<>I/K2 THENF.Q=K3 TOK10:F$(Q,Q)=CHR$(ASC(F$(Q))+K128):N.Q
2720POS.K2,I:?" ";F$(K3,K10);"     ":N.I:RET.
2730F.Q=I TOK10:POS.K2,Q:?"            ":N.Q:RET.

; (W) WRITE (BASIC, DATA, .BYTE)

3000?"WHICH   WRITE?          1) BASIC2) DATA 3).BYTE":IFHF THENC$(Q513,Q520)=HEART$:HF=K0
3010POK.INVFLG,K0:GOS.K5:G=K:IFG<Q49 ORG>51THEN3010
3020IFG=Q49 THENG.L3065
3030T.3030:B=94:GOS.L440-K5:GOS.L440:IFDISK=K0 THENTEMP$="C:":G.3045
3035?" NAME OF FILE TO  WRITE";:GOS.K128:IFF$=""THEN?" CANCEL  WRITE";:G.3910
3040TEMP$="D:":TEMP$(K3)=F$:TEMP$(LEN(TEMP$)+K1)=".DAT":IFG=51THENTEMP$(LEN(TEMP$)-K3)=".BYT"
3045T.3045:GOS.L440:?"STARTING LINE #";:I.LN
3050T.3050:GOS.L440:?" INCRE-  MENT    BETWEEN LINES ";:I.CT:GOS.L440
3055?" BEGIN   WITH    WHAT    LETTER?";:GOS.L440+K32:IN=PEEK(DL+288):M=IN*K8
3060T.3060:GOS.L440:?" HOW     MANY   LETTERS";:I.IN1:IN2=INT(IN1/K3):IFIN1<K0 ORIN1>128THEN3370

; WRITE BASIC - ESC to cancel

3065GOS.L440:CL.#K5:?HR$:GOS.K5:B=K:GOS.L440:IFB=K27 THEN?:?" CANCEL  WRITE":G.3910
3070ONG-Q49 GOTO3400,3500

; Create LOADSET.LST to load the character set

3100T.L3065:LN=30100:CT=K0:TEMP$="D:LOADSET.LST":B=ADR(C$):IFDISK=K0 THENTEMP$="C:":POK.CH,K12
3120O.#K5,8,K0,TEMP$:?#K5;"0DIMQQ$(1):GOS.30000"
3130?#K5;"30000VT=PEEK(134)+256*PEEK(135):AT=PEEK(140)+256*PEEK(141):B=PEEK(106):IFB/2=INT(B/2)THENPOK.106,B-5"
3140?#K5;"30010BASE=PEEK(106)+1:OFFSET=256*BASE-AT:V3=INT(OFFSET/256):V2=OFFSET-256*V3"
3150?#K5;"30020POK.VT+2,V2:POK.VT+3,V3:POK.VT+4,0:POK.VT+5,4:POK.VT+6,0:POK.VT+7,4"
3160?#K5;LN;"QQ$=";CHR$(K34);:F.Q=K0 TO33:C=B+Q:IF(PEEK(C)=Q155 ORPEEK(C)=K34)THENGOS.3280:G.3180
3170PUT#K5,PEEK(C)
3180N.Q:?#K5
3190F.X=K0 TOK10:CT=K0:LN=30200+100*X:?#K5;LN;"QQ$(LEN(QQ$)+1)=";CHR$(K34);:F.Q=K0 TO89:C=B+Q+90*X+34
3200IF(PEEK(C)=Q155 ORPEEK(C)=K34)THENGOS.3280:G.3220
3210PUT#K5,PEEK(C)
3220N.Q:?#K5:N.X:?#K5;"31300GR.0:POK.756,BASE:RET.":G.L3900
3280?#K5:?#K5;LN+CT+K1;"QQ$(LEN(QQ$)+1)=CHR$(";PEEK(C);")":?#K5;LN+CT+K2;"QQ$(LEN(QQ$)+1)=";CHR$(K34);
3290CT=CT+K2:RET.

3400T.L3065:CL.#K5:POK.CH,K12:O.#K5,K8,K0,TEMP$:IFIN2<K1 THEN3430
3410F.I=K0 TOIN2-K1:?#K5;LN+CT*I;"D.";:F.J=K1 TO23
3420?#K5;ASC(C$(M+J+Q24*I));",";:N.J:?#K5;ASC(C$(M+Q24+Q24*I)):N.I
3430IN3=K8*(IN1-IN2*K3):M=M+IN2*Q24:IFIN3<K7 THENG.L3900
3440?#K5;LN+CT*IN2;"D.";:F.J=K1 TOIN3-K1:?#K5;ASC(C$(M+J));",";:N.J:?#K5;ASC(C$(M+IN3)):G.L3900

3500T.L3065:CL.#K5:POK.CH,K12:O.#K5,K8,K0,TEMP$:F.I=K0 TOIN1-K1:?#K5;LN+CT*I;"  .BYTE ";:F.J=K1 TOK7
3510N=ASC(C$(M+J+K8*I)):GOS.3950:?#K5;",";:N.J:N=ASC(C$(M+K8+K8*I)):GOS.3950:?#K5:N.I
3900CL.#K5:?:?" WRITE   DONE"

; Finished the operation. Enable DLI,

3910POK.NMIEN,Q192:GOS.L425:GOS.L440:GOS.L445:T.DOOR:FLAG=K1:POK.CH,K255:G.K200
3950?#K5;"$";:B=INT(N/K16):GOS.3960:B=N-B*K16
3960?#K5;CHR$(B+K48+K7*(B>9));:RET.

; Integers for smaller (faster?) BASIC

29000K0=0:K1=1:K2=2:K3=3:K4=4:K5=5:K6=6:K7=7:K8=8:K10=10:K11=11:K12=12:K14=14:K15=15:K16=16:K20=20:K23=23
29010K27=27:K28=28:K32=32:K34=34:K64=64:Q82=82:K128=128:K200=200:K255=255:K256=256:K295=295:L440=440:CH=764
29012DOOR=40000:Q24=24:K40=40:K48=48:Q49=49:L3065=3065:Q83=83:Q155=155:Q156=156:Q192=192
29014CONSOL=53279:NMIEN=54286:L425=425:L445=445:Q513=513:Q520=520:SDMCTL=559:SDLSTL=560:SDLSTH=561:INVFLG=694:PCOLR3=707
29016CRSINH=752:A1791=1791:L3900=3900:L965=965
29017DIMHR$(30):HR$=" PREP    DISK &  PRESS   RETURN"

; X = VVTP, G = STARP (program end), B = RAMTOP

29020X=PEEK(134)+K256*PEEK(135):G=PEEK(140)+K256*PEEK(141):B=PEEK(106):POK.106,B-K8:BASE=B-K4:GR.K0

; Turn off DMA to speed up init. Inhibit the cursor.

29025POK.SDMCTL,K0:POK.CRSINH,K1

; M = RAMTOP-STARP, S = Free Pages, T = Remainder

29030M=K256*(BASE-K4)-G:S=INT(M/K256):T=M-K256*S:F.I=K0 TOK5:F.J=K0 TOK3:POK.X+K4+J+K8*I,K0:N.J:N.I

; Hack the VVTP to relocate strings to allocated memory
; Then we can use strings to draw Player/Missile objects

; Reloc PM$
29040POK.X+K2,T:POK.X+K3,S
; Reloc P0$
29041POK.X+K10,T:POK.X+K11,S+K2
; Reloc P2$
29042POK.X+26,T:POK.X+27,S+K3
; Reloc C$
29043POK.X+42,T:POK.X+43,S+K4
; Redim PM$(2)
29060POK.X+K5,K2:POK.X+K7,K2
; Redim C$(4)
29061POK.X+45,K4:POK.X+47,K4
; Redim P0$(128), $P1(128), $P2(128), $P3(128)
29062F.I=K1 TOK4:F.J=K0 TOK2 STEPK2:POK.X+I*K8+J+K4,K128:N.J:N.I

29070M=M+640:S=INT(M/K256):T=M-K256*S
; Reloc P1$
29071POK.X+18,T:POK.X+19,S
; Reloc P3$
29072POK.X+K34,T:POK.X+35,S+K1

; Count uo the number of mounted disks
30040DISK=K0:F.I=809TO830STEPK3:DISK=DISK+(PEEK(I)=68):N.I

; More common strings
30050DIM TEMP$(K32),HEART$(K8),F$(K16),P$(K20),CUR$(K14):P$=">SELECT FROM MENU>"

; Magic clear PM$ by assigning element 2 to element 1
; Open the K: device
30070PM$="":PM$(512)="":PM$(K2)=PM$:CL.#K1:O.#K1,K4,K0,"K:"

; Copy the character set to our base
31100X=BASE*K256:I=USR(ADR("hhh"),X):HEART$=C$(Q513,Q520):POK.A1791,BASE
31310POK.K16,K64:DL=PEEK(SDLSTL)+K256*PEEK(SDLSTH):POK.DL+22,K3:POK.DL+K23,K4:POK.DL+Q24,K6:POK.DL+25,K5
31320POK.53744,K64:POK.DL+26,K7:POK.DL+K27,65:POK.DL+K28,PEEK(SDLSTL):POK.DL+29,PEEK(SDLSTH):POK.DL+12,130
31330POK.708,196:POK.709,148:POK.710,K14:POK.711,52
31340POK.Q82,K27:POS.K27,K1:?"    EEDIT    ";
31350?"LLOAD          ";:GOS.32000
31360F.I=K0 TOK3:F.Q=K0 TO31:POK.DL+516+K40*I+Q,Q+32*I:N.Q:N.I:C=63:IN=31:GOS.800:FLAG=K1:POK.CH,K255
31370P0$=PM$:P1$=P0$:P2$=P0$:P3$=P0$:POK.704,44:POK.705,132:POK.706,52

; Quadruple-size Players and Missiles

31380POK.53257,K3:POK.53258,K3:POK.53259,K3:POK.53260,K255

; Set positions for all 8 objects

31385TEMP$="vo_o":HP=ASC(TEMP$(K1,K1)):F.I=K1 TOK8:POK.53247+I,ASC(TEMP$(I,I)):N.I
31390P2$(21)="":PM$(405)="PPPP":VP=K20
31400CUR$="xHHx":P3$(25)=""
31420GOS.435:?"INSTEDIT (C)1981 by S.   Leemon":GOS.L445:POK.PCOLR3,Q156
31425DLI=ADR("Hh@"):DLIH=INT(DLI/K256):DLIL=DLI-DLIH*K256:POK.512,DLIL:POK.Q513,DLIH

; Set PMBASE, GRACTL, GPRIOR

31430POK.54279,BASE-K4:POK.53277,K3:POK.623,K48
31435POK.SDMCTL,46:POK.NMIEN,Q192:G.200
32000POK.82,K2:POK.Q83,K12:POS.K2,K1:?"BBLANKIINVERT";
32010?"RRESTORE";:POK.Q83,39:RET.
