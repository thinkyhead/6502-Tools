;
; Microtext 1.2
; Softside Vol 5-07
; April 1982
; Disk #43
;
10 DIM CL$(1):CL$=CHR$(125):?CL$
15 POKE 752,1
20 POSITION 8,8:?"M I C R O T E X T   1 . 2"
30 POSITION 12,12:?"BY JON R. VOSKUIL"
40 ?:?"COPYRIGHT 1982  SOFTSIDE PUBLICATIONS"
50 FOR Z=1 TO 1000:NEXT Z

; INITIALIZATION

100 ?CL$:TRAP 20000
120 DIM L$(40),T$(14000),B$(1),C$(1),CR$(1),LIN$(37),LNXT$(40),F$(14),S$(37),FT$(14),LP(500),TT$(40),X$(5)
125 DIM P$(255),PP$(80),Q$(37),F1$(14)
130 BKSP=126:RTN=155:SPC=32:B$=CHR$(126):CR$=CHR$(20)
135 T$="":L$="":LP(0)=0
140 CHAR=1
150 LN=1
160 LIN$="-------------------------------------":S$="                                     "
170 OPEN #1,4,0,"K:"
180 LWID=36
185 POKE 752,0
190 V=2:H=2
200 POSITION 2,0:?"^SAVE  ^LOAD  ^REVIEW  ^EDIT  ^PRINT";
210 POSITION 2,1:?LIN$;
220 POSITION H,V:?" ";B$;

; INPUT LOOP

500 GET #1,C:C$=CHR$(C)
520 IF C=RTN THEN C$=CR$

; BACKSPACE

640 IF C<>BKSP THEN 720
650 IF CHAR<2 THEN L$="":GOTO 500
660 ?B$;" ";B$;
670 CHAR=CHAR-1
675 IF CHAR<2 THEN 500
680 L$=L$(1,CHAR-1)
700 GOTO 500

; CHK FOR CTRL CHAR

720 IF C<SPC THEN 2000

; CHK FOR END OF LN

740 CHAR=CHAR+1:IF CHAR<LWID OR C=SPC OR C=RTN THEN 760
750 GOSUB 1000

; ADD TO STRING

760 L$(CHAR-1)=C$

; RETURN

780 IF C<>RTN THEN 880
820 ?C$
840 GOSUB 6000:SLOC=0
850 CHAR=1
860 GOTO 500

; ?ON SCRN

880 ?C$;

; UPDATE SPC PNTER, CHK FOR END OF LINE

900 IF C=32 THEN SLOC=CHAR-1:IF CHAR=LWID THEN GOSUB 6000:CHAR=1:SLOC=0:?
920 GOTO 500
1000 IF SLOC=0 THEN ?:GOTO 1100
1020 SS=LWID-SLOC-1:FOR J=1 TO SS:?B$;:NEXT J
1040 FOR J=1 TO SS:?" ";:NEXT J:?
1050 IF SLOC=LEN(L$) THEN LNXT$="":GOTO 1100
1060 LNXT$=L$(SLOC+1)
1080 L$=L$(1,SLOC)
1100 GOSUB 6000
1110 L$=LNXT$
1115 LNXT$=""
1120 ?L$;
1140 CHAR=LEN(L$)+2
1150 SLOC=0
1160 RETURN

; PROCESS CTRL CHAR

2000 V=PEEK(84):H=PEEK(85)
2050 TL=LEN(T$)
2060 T$(TL+1)=L$
2070 POSITION 2,0:?S$:?LIN$;
2080 IF C<>6 THEN 2100:. CTRL-F (BACK TO EDIT)
2085 IF CHAR>1 THEN LP(LN)=LEN(T$):LN=LN+1:LP(LN)=LEN(T$):L$=""
2090 RETURN
2100 IF C=18 THEN GOSUB 3000:. CTL-R
2200 IF C=19 THEN GOSUB 4000:. CTL-S
2300 IF C=12 THEN GOSUB 5000:. CTL-L
2400 IF C=17 THEN END :. CTL-Q
2500 IF C=16 THEN GOSUB 7000:. CTL-P
2600 IF C=5 AND LN>1 THEN I=LN-1:VV=V:POSITION 2,VV-1:GOSUB 9000:GOSUB 3000:. CTL-E
2900 IF TL>0 THEN T$=T$(1,TL)
2950 GOTO 200

; REVIEW TEXT

3000 ?CL$;"Press any key to pause":?LIN$
3040 IF LN=1 THEN 3210
3050 FOR I=1 TO LN-1
3055 FOR Z=1 TO 20:NEXT Z
3060 ?T$(LP(I-1)+1,LP(I))
3070 IF PEEK(764)=255 AND  NOT STP THEN 3200
3080 STP=0:POKE 764,255
3090 VV=PEEK(84)
3100 POSITION 2,1:?LIN$;:POSITION 2,0:?"  RTN:Cont     SPC:Stp     E:Edit   ";
3120 X=PEEK(764):POKE 764,255
3125 IF X=42 THEN GOSUB 9000:GOTO 3000
3160 IF X=12 THEN 3190
3170 IF X<>33 THEN 3120
3180 STP=1
3190 POSITION 2,VV:POKE 764,255
3200 NEXT I
3210 X=LP(LN-1)+1:IF X<=LEN(T$) THEN ?T$(X);
3220 H=PEEK(85):V=PEEK(84)
3230 RETURN

; SAVE TO DISK/TAPE

4000 POSITION 2,0:?S$;:POSITION 2,0:?"Save to Tape or Disk? (T/D/ESC) ";
4020 GET #1,X
4030 IF X=27 THEN 4400
4060 IF X=84 THEN 4200
4070 IF X<>68 THEN 4000
4075 F1$=F$
4080 POSITION 2,0:?S$;:POSITION 2,0:?"File Name: ";:INPUT F$
4082 IF F$="" AND F1$="" THEN 4080
4083 IF F$="" THEN F$=F1$
4085 IF F$(1,1)<>"D" THEN FT$="D:":FT$(3)=F$:F$=FT$
4090 POSITION 2,0:?"Insert disk and press RETURN";:GET #1,X
4100 OPEN #2,8,0,F$:GOTO 4210
4200 POSITION 2,0:?"Start tape recorder and press RETURN";:GET #1,X
4205 OPEN #2,8,0,"C:"
4210 ?#2;LN:?#2;SLOC:?#2;CHAR
4220 FOR I=1 TO LN-1
4230 ?#2;T$(LP(I-1)+1,LP(I))
4240 NEXT I
4250 IF CHAR>1 THEN ?#2;T$(LP(LN-1)+1)
4300 CLOSE #2
4400 RETURN

; LOAD FROM DISK/TAPE

5000 POSITION 2,0:?S$;:POSITION 2,0:?"Load from Tape or Disk? (T/D/ESC) ";
5020 GET #1,X
5030 IF X=27 THEN 5400
5060 IF X=84 THEN 5200
5070 IF X<>68 THEN 5000
5075 F1$=F$
5080 POSITION 2,0:?S$;:POSITION 2,0:?"File Name: ";:INPUT F$
5082 IF F$="" AND F1$="" THEN 5080
5083 IF F$="" THEN F$=F1$
5085 IF F$(1,1)<>"D" THEN FT$="D:":FT$(3)=F$:F$=FT$
5090 POSITION 2,0:?"Insert disk and press RETURN";:GET #1,X
5100 OPEN #2,4,0,F$:GOTO 5210
5200 POSITION 2,0:?"Start tape recorder and press RETURN";:GET #1,X
5205 OPEN #2,4,0,"C:"
5210 INPUT #2;LN:INPUT #2;SLOC:INPUT #2;CHAR
5215 T$=""
5220 FOR I=1 TO LN-1
5230 INPUT #2,TT$:T$(LEN(T$)+1)=TT$
5240 LP(I)=LEN(T$)
5250 NEXT I
5255 TL=LEN(T$):L$=""
5260 IF CHAR>1 THEN INPUT #2,TT$:T$(LEN(T$)+1)=TT$:LP(LN)=LEN(T$):L$=TT$
5300 CLOSE #2
5350 GOSUB 3000
5400 RETURN

; ADD LN TO TEXT STRING

6000 T$(LEN(T$)+1)=L$
6080 LP(LN)=LEN(T$)
6100 LN=LN+1:LP(LN)=LEN(T$)
6150 L$=""
6200 RETURN

; PRINTOUT RTN

7000 ?CL$:POSITION 2,6:LIN=0
7010 ? "Left margin? (Default = 10) ";:INPUT X$:LM=10:IF LEN(X$)>0 THEN IF VAL(X$)>0 THEN LM=VAL(X$):IF LM>37 THEN LM=37
7020 ?:?"Right margin? (Default = 70) ";:INPUT X$:RM=70:IF LEN(X$)>0 THEN IF VAL(X$)>0 THEN RM=VAL(X$)
7030 ?:?"Line spacing? (Default = 2) ";:INPUT X$:LS=2:IF LEN(X$)>0 THEN IF VAL(X$)>0 THEN LS=VAL(X$)
7040 LL=RM-LM
7070 ?CL$:LP."":P$="":CR=0:I=0
7080 I=I+1:P$(LEN(P$)+1)=T$(LP(I-1)+1,LP(I))
7090 IF P$(LEN(P$))=CR$ THEN CR=1:GOTO 7110
7100 IF LEN(P$)<255-LWID AND I<LN-1 THEN 7080
7110 GOSUB 7500:CR=0
7120 IF I<LN-1 THEN 7080
7130 LP.S$(1,LM);L$;
7150 LP.""
7160 GOSUB 3000
7170 RETURN
7500 L=LL
7510 IF LEN(P$)>LL THEN 7550
7520 IF ( NOT CR) THEN 7640
7530 LP=LEN(P$):IF LP<2 THEN PP$="":P$="":GOTO 7590
7540 PP$=P$(1,LP-1):P$="":GOTO 7590
7550 C$=P$(L,L):IF C$=" " THEN 7580
7560 L=L-1:IF L>0 THEN 7550
7570 L=LL
7580 PP$=P$(1,L):P$=P$(L+1)
7590 LP.S$(1,LM);PP$;
7610 FOR J=1 TO LS:LIN=LIN+1:LP."":NEXT J
7615 IF LIN>59 THEN FOR J=1 TO 66-LIN:LP."":NEXT J:LIN=0
7620 IF LEN(P$)>LL THEN L=LL:GOTO 7550
7630 IF CR AND LEN(P$)>0 THEN 7530
7640 RETURN

; TEXT REJUSTIFY RTN

8000 ?CL$:POSITION 2,5:?"Re-justifying text. . ."
8010 TL=LEN(T$):N=EL+NL-1:C=LP(N)+1:SL=C-1:CH=C-LP(N-1)
8020 C$=T$(C,C)
8030 IF C$=CR$ THEN 8100
8035 IF C=TL THEN FL=1:SLOC=SL-LP(N-1):GOTO 8100
8040 IF C$=" " THEN SL=C
8050 IF CH<LWID-1 THEN CH=CH+1:C=C+1:GOTO 8020
8060 IF SL=LP(N-1) THEN SL=C
8070 LP(N)=SL:C=SL+1:N=N+1:CH=1:GOTO 8020
8100 LP(N)=C
8110 IF LP(N)=LP(N+1) THEN LN=LN-1:FOR I=N TO LN-1:LP(I)=LP(I+1):NEXT I
8120 RETURN

; EDIT SUBR

9000 FL=0:IF CHAR>1 THEN LP(LN)=LEN(T$):LN=LN+1:LP(LN)=LEN(T$):L$="":FL=1
9005 POKE 752,1:IT=I:IF I>21 THEN V1=22:GOTO 9040
9010 V1=I+1:POSITION 2,VV
9020 X=21:IF X>LN-1 THEN X=LN-1
9025 IF X=IT THEN 9040
9030 FOR I=IT+1 TO X:?T$(LP(I-1)+1,LP(I)):NEXT I
9040 EL=V1+(IT>21)*(IT-21)-1
9050 Q$="UP/DN:Move RTN:Edit  D,X:Del ESC:Exit"
9060 POSITION 2,0:?Q$:?LIN$
9080 C=ASC(T$(LP(EL-1)+1)):POSITION 2,V1:?CHR$(C+128);:GET #1,X
9085 POSITION 2,V1:?CHR$(C);
9090 IF X<>45 THEN 9130:. UP
9100 IF V1>2 THEN V1=V1-1:EL=EL-1:GOTO 9080
9110 IF EL=1 THEN 9080
9115 EL=EL-5:IF EL<1 THEN EL=1
9118 NN=20:IF EL+NN>LN-1 THEN NN=LN-EL-1
9120 POSITION 2,2:FOR I=EL TO EL+NN:? S$;"";T$(LP(I-1)+1,LP(I)):NEXT I:GOTO 9080
9130 IF X<>61 THEN 9180:. DOWN
9140 IF EL>=LN-1-FL THEN 9080
9150 EL=EL+1
9160 IF V1<22 THEN V1=V1+1:GOTO 9080
9165 NN=4:IF NN>LN-EL-1-FL THEN NN=LN-EL-1-FL
9170 EL=EL+NN:POSITION 2,23:FOR I=EL-NN TO EL:?T$(LP(I-1)+1,LP(I)):NEXT I:GOTO 9060
9180 IF X=27 THEN 9580:. ESC
9190 IF X<>68 OR V1=2 THEN 9250:. D
9200 NC=LP(EL)-LP(EL-1):IF EL=LN-1 THEN T$=T$(1,LP(EL-1)):GOTO 9205
9202 T$(LP(EL-1)+1)=T$(LP(EL)+1)
9205 FOR J=EL TO LN-1:LP(J)=LP(J+1)-NC:NEXT J
9210 X=22-V1:IF X>LN-EL-2 THEN X=LN-EL-2
9220 POSITION 2,V1:IF EL<LN-1 THEN FOR J=EL TO EL+X:?S$;"";T$(LP(J-1)+1,LP(J)):NEXT J
9225 ?S$;
9230 IF EL=LN-1-FL THEN V1=V1-1:EL=EL-1
9240 LN=LN-1:GOTO 9080
9250 IF X<>88 THEN 9310:. X
9260 POSITION 2,0:?"Delete from here to the end of text?";:GET #1,X:IF X<>89 THEN 9060
9270 LN=EL:L$="":CHAR=1:SLOC=0:IF LN>1 THEN T$=T$(1,LP(LN-1)):GOTO 9280
9275 T$=""
9280 TL=LEN(T$):GOTO 9580
9310 IF X<>155 THEN 9080

; EDIT LINE

9320 L1=EL-8:IF L1<1 THEN L1=1
9330 L2=EL+8:IF L2>LN-1 THEN L2=LN-1
9340 ?CL$;"Type new line below (^F to finish)  ":?LIN$
9350 FOR J=L1 TO EL:?T$(LP(J-1)+1,LP(J)):NEXT J
9360 ?:?:?:?
9370 IF L2>EL THEN FOR J=EL+1 TO L2:?T$(LP(J-1)+1,LP(J)):NEXT J
9380 POKE 752,0:POSITION 2,EL-L1+2
9390 TLN=LN
9410 C1=CHAR:S1=SLOC:CHAR=1:SLOC=0
9420 GOSUB 500
9430 CHAR=C1:SLOC=S1
9450 NL=LN-TLN:NC=LP(LN-1)-LP(TLN-1)
9480 IF EL=LN-1 THEN T$=T$(1,LP(EL-1)):GOTO 9490
9485 T$(LP(EL-1)+1)=T$(LP(EL)+1)
9490 CC=LP(EL)-LP(EL-1):FOR J=EL TO LN-1:LP(J)=LP(J+1)-CC:NEXT J:IF NL=0 THEN LN=TLN-1:GOTO 9580
9500 X$=T$(LEN(T$)):IF X$<>CR$ AND X$<>" " THEN T$(LEN(T$)+1)=" ":NC=NC+1:LP(LN-2)=LP(LN-2)+1:LP(LN-1)=LP(LN-1)+1
9502 CN=LP(EL-1):FOR I=LEN(T$) TO CN+1 STEP -NC:IF I<NC THEN T$(CN+NC+1,I+NC)=T$(CN+1,I):GOTO 9504
9503 T$(I+1,I+NC)=T$(I-NC+1,I)
9504 NEXT I
9505 T$(CN+1,CN+NC)=T$(LEN(T$)-NC+1)
9506 FOR I=LN-2 TO EL STEP -1:LP(I+NL)=LP(I)+NC:NEXT I
9508 J=TLN+NL-1:K=LP(LN-2)-LP(EL-1):FOR I=0 TO NL-1:LP(EL+I)=LP(J+I)-K:NEXT I
9510 T$=T$(1,LEN(T$)-NC)
9530 LN=TLN+NL-1:IF X$=CR$ OR EL=LN-NL THEN 9580
9550 SS=1:P$=T$(LP(EL+NL-1)+1,LP(EL+NL)):LL=LEN(P$)
9560 IF P$(SS,SS)<>" " AND SS<LL THEN SS=SS+1:GOTO 9560
9570 IF LP(EL+NL-1)-LP(EL+NL-2)+SS<=LWID THEN GOSUB 8000
9580 TL=LEN(T$):IF FL THEN LN=LN-1:L$=T$(LP(LN-1)+1,LP(LN)):CHAR=LEN(L$)+1:TL=LP(LN-1)
9600 STP=0:POKE 752,0:RETURN

; ERROR-HANDLING RTN

20000 CLOSE #2:E=PEEK(195)
20010 POSITION 2,0:?S$;:POSITION 2,0:?"Error: Code ";E;"; press any key";
20020 GET #1,X
20030 TRAP 20000
20040 GOTO 200
