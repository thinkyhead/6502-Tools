0 .
0 . MASTMIND.LST
0 . AtariBASIC MasterMind game
0 . by Thinkyhead
0 .

10 CLR:L=4:N=6:DIM K$(1),G$(L),A$(L),UG(L),UA(L)

20 ?CHR$(125);"MASTERMIND"
21 ?"I'm thinking of a ";L;"-digit number"
22 ?" using the digits 1 to ";N;"."
23 ?" Good luck!":?

0 .New Sequence: L length using N digits
30 TURN=0:A$="":F.I=1TOL:V=INT(RND(0)*N):A$(I,I)=CHR$(49+V):N.I

0 .Next turn
40 TURN=TURN+1:C=0:IFTURN<10THEN?" ";
41 ?TURN;"> ";
42 GOS.100:ONK$="Q"GOTO200:ONK$="A"GOTO150:IFK$<"1"ORK>48+N THEN42
43 ?K$;" ";:C=C+1:G$(C,C)=K$:IFC<L THEN42

0 .Prepare to check the answer
50 PM=0:XM=0:F.J=1TOL:UG(J)=0:UA(J)=0:N.J
0 .Count up exact matches
51 F.I=1TOL:IFG$(I,I)=A$(I,I)THENUG(I)=1:UA(I)=1:XM=XM+1
0 .Count up out-of-order matches
52 N.I:F.I=1TOL:ONUG(I)GOTO55:F.J=1TOL:ONUA(J)GOTO54
53 UG(I)=1:IFG$(I,I)=A$(J,J)THENUA(J)=1:PM=PM+1:G.55
54 N.J
55 N.I

0 .How did you do? Keep playing till you win.
70 ?"(";XM;"|";PM;")":IFXM<L THEN40

80 ?"You won in ";TURN;" moves!":?:G.21

0 .Get a key
100 OPEN #1,4,0,"K:":GET #1,K:CL.#1:K$=CHR$(K):RET.

0 .Cheat
150 ?:?"Answer is ";A$:?:G.21

0 .Quit
200 ?:?"QUIT":END

0
RUN
