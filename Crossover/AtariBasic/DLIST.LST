100G.2000
102 DIM A$(10):DONE=0:PV=-1:PC=0
105 DIM G$(8),M$(15):G$="TEXTGRFX":M$=" 0***123456*7*8"
110 DL=PEEK(560)+256*PEEK(561):? "Display List at $";:A=560:GOS.1310:? " (";DL;")"
120 F.I=0TO255:D=DL+I:V=PEEK(D):GOS.1000:IFNOTDONE THENN.I
130 END

1000 IFPV=V THENPC=PC+1:RET.
1001 PV=V:IFPC THEN?"         (...x";PC+1;")":PC=0
1005 A=D:GOS.1320:? " ";
1010 NH=INT(V/16):NL=V-NH*16:TH=NH
1011 DLI=TH>=8:IFDLI THENTH=TH-8
1012 LMS=TH>=4:IFLMS THENTH=TH-4
1013 VSC=TH>=2:IFVSC THENTH=TH-2
1014 HSC=TH
1020 IFV<100 THEN?" ";:IFV<10 THEN?" ";
1021 ? V;" ";
1100 IFNL=0 THEN?"SKIP";NH+1-(NH>7)*8:RET.
1110 IFNL=1 AND LMS THEN?"JWVB";:DONE=1:G.1201
1111 IFNL=1 THEN?"JUMP";:G.1201
1200 N=1+4*(NL>8):? G$(N,N+3);M$(NL,NL);
1201 IFDLI THEN?"+DLI";
1202 IFLMS THEN?"+LMS(";:GOS.1300:? ")";
1203 IFVSC THEN?"+VSCROL";
1204 IFHSC THEN?"+HSCROL";
1205 ? :RET.
1300 A=D+1:I=I+2
1310 X=PEEK(A+1):GOS.1330:X=PEEK(A):G.1330
1320 X=INT(A/256):GOS.1330:X=A-X*256
1330 N=INT(X/16):GOS.1340:N=X-N*16
1340 C=N+48:IFC>57 THENC=C+7
1341 ? CHR$(C);:RET.

0 . Look at some registers 
2000 GOS.3000:SZ=(B-4)*256:IFSZ<=PEEK(144)+256*PEEK(145)THEN?"LOW MEMORY!":STOP
2001 GOS.5000:POKE106,B-4:GR.0:.MOVE DL DATA
2002 GOS.3000:END

3000 A=PEEK(512)+256*PEEK(513):?"VDSLST=";A:.NMI for DLI
3001 B=PEEK(106):?"MEMTOP=";B
3002 C=PEEK(14)+256*PEEK(15):?"APPMHI=";C
3003 ?"FREE=";B*256-C
3004 DL=PEEK(560)+256*PEEK(561):?"SDLSTL=";DL
3999 RET.

5000 ?"?";:O.#3,4,0,"K:":GET#3,K:CL.#3:?:RET.

9998 END
9999 S."D:DLIST.BAS":? "Saved!":END

0
