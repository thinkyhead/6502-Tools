; AtariBASIC
;
; SWITCHBX.LST
;
; Switchbox by Todd Heimarck
; Atari version by Kevin Mykytyn
; Compute! Magazine Issue 070
;
0 N0=0:N1=1:G.100

;
; Print M$ at Position CX,CY
;
1 .CHAR COMMAND
2 POS.CX,CY:?M$;:RET.

100 O.#N1,4,N0,"K:":SCR=PEEK(88)+256*PEEK(89):POKE82,N0:POKE752,N0
120 DIM SW(4,7),SX(4,7),SP$(6),LB(32,4),AR$(6),PT(4,16),SC(N1,8),P1$(20),P2$(20)
125 DIM M$(20),T$(20),Y$(10),R$(10),L$(10),A$(5)
127 F.A=0TO1:F.B=0TO8:SC(A,B)=N0:N.B:N.A
130 SP$(N1,3)="":SP$(4,6)="":AR$(N1,3)="<  ":AR$(4,6)="  >":QR=N1
140 SE.4,3,2:SE.2,N0,8:SE.N1,N0,N0
150 F.J=1TO4:READQ:PT(J,N0)=Q:.Name and Goal
160 F.K=1TO8:READL:PT(J,K+8)=L:PT(J,9-K)=L:N.K:N.J:.Points
170 D.10
180 D.2,2,2,2,2,2,2,2
190 D.40
200 D.1,2,3,5,8,13,21,34
210 D.20
220 D.2,3,4,5,6,7,8,9
230 D.80
240 D.1,4,9,16,25,36,49,64

;
; Get player names
;

250 ?"":?"PLAYER 1 ";:I.P1$
260 ?"PLAYER 2 ";:I.P2$:IFLEN(P1$)>5THENP1$=P1$(N1,5)
264 IFLEN(P2$)>5THENP2$=P2$(N1,5)
266 ?:?P1$;" VS ";P2$
270 ?"IS THIS CORRECT?":GET#N1,A:IFCHR$(A)<>"Y"THEN250


280 POKE752,N1:GOS.450:GOS.610:.Setup

;
; Game Main Loop
;

290 F.RR=1TO4:TX=SCR+48+40*RR:POKETX,96:POKETX+22,96
300 GOS.560:.Put scores at bottom

;
; Loop for a round
;

310 QR=N1-QR:TY=QR*20:TX=28-TY:CX=TX:CY=N0
320 M$=STR$(PT(RR,N0)):M$(3,3)=" "
330 GOS.2:TX=8+TY:CX=TX:CY=N0:M$=AR$(QR*3+N1,QR*3+3):GOS.2
340 GOS.770:IF SC(N1-QR,RR)>=PT(RR,N0)THEN360:.End of round
350 G.310

;
; End of a round
;

360 F.J=0TO1:F.K=5TO8:SC(J,K)=N0:N.K:N.J
370 F.J=0TO1:F.K=1TO4:GL=PT(K,N0):AC=SC(J,K):SC(J,5)=SC(J,5)+AC
380 SC(J,6)=SC(J,6)+(AC>=GL)*GL:SC(J,7)=SC(J,7)+(SC(J,K)-SC(N1-J,K)):N.K:N.J
390 F.J=0TO1:F.K=6TO7:SC(J,K)=SC(J,K)+SC(J,5):N.K:N.J
400 F.J=0TO1:F.K=5TO7:SC(J,8)=SC(J,8)+SC(J,K):N.K:N.J
410 F.J=0TO1:F.K=5TO8:Y$=STR$(SC(J,K)):L=LEN(Y$):TX=6+J*31-L
420 TY=3+K:CX=TX-(TX<20):CY=TY:M$=Y$:GOS.2:N.K:N.J
430 N.RR:.End of main loop

;
; Wait for key to restart
;

440 GET#N1,TK:RUN

;
; Set up the playfield
;

450 ?"";:?"           ";:F.J=1TO7:?"";:N.J:?"":LL=7
460 F.J=0TO4:TX=9-2*J:TY=N1+J*4:R$=" "
470 F.K=1TO2:CX=TX:CY=TY+K-N1:M$="":GOS.2
480 ?"   ";:GOS.550:?"":N.K
490 CX=TX:CY=TY+K-N1:M$="":GOS.2
500 ?"  ";:GOS.550:?"":CX=TX:CY=TY+K:M$="":GOS.2
510 LL=LL+2:?"";:GOS.550:?"";:N.J
520 ?:?" ";:GOS.550:?""
530 R$="":LL=LL+N1:?" ";:GOS.550:?""
540 RET.


550 F.L=1TOLL:?R$;:N.L:RET.


560 F.J=1TO16:K=PT(RR,J):JJ=2+J*2
570 IFK>9THENL=INT(K/10):T$=STR$(L):L$=T$(N1,N1):G.590
580 L$=CHR$(32)
590 CX=JJ:CY=22:M$=L$:GOS.2:CX=JJ:CY=23:T$=STR$(K):M$=T$(LEN(T$),LEN(T$)):GOS.2
600 N.J:RET.

;
; Set up for a new game
;

610 F.J=0TO4:SY=4+J*4:F.K=0TOJ+3:SX=12-J*2+K*4
620 CX=SX+N1:CY=SY-N1:M$=" ":GOS.2
630 M$=" ":CX=SX+N1:CY=SY-N1:GOS.2:WP=INT(RND(N1)*2)
640 SW(J,K)=WP:SX(J,K)=N0:GOS.760
650 N.K:N.J
660 F.J=1TO8:POKESCR+50+J*2,16+J:N.J
670 F.J=0TO1:BX=J*31:CX=BX:CY=N0:M$="":GOS.2
680 ?"";
690 F.K=1TO5:CX=BX:CY=K:M$="":GOS.2:?"      ";:N.K
700 CX=BX:CY=K:M$="":GOS.2
710 ?"";
720 N.J:F.TK=1TOLEN(P1$):P1$(TK,TK)=CHR$(ASC(P1$(TK,TK))+128):N.TK
725 F.TK=1TOLEN(P2$):P2$(TK,TK)=CHR$(ASC(P2$(TK,TK))+128):N.TK
730 CX=3-(LEN(P1$)=5):CY=N0:M$=P1$:GOS.2
740 CX=34-(LEN(P2$)=5):CY=N0:M$=P2$:GOS.2
750 RET.


760 CX=SX:CY=SY:M$=SP$(WP*3+N1,WP*3+3):GOS.2:RET.


770 F.J=0TO32:LB(J,N0)=N0:N.J:NB=N1
780 GET#N1,A:A$=CHR$(A)
790 IFA$="-"THENRET.
800 IFA$="+"THENA$=STR$(INT(RND(N1)*8+N1))
805 IFA$<"1"ORA$>"8"THEN780
810 A=VAL(A$)
820 LB(N0,N0)=N1:F.J=1TO3:LB(N0,J)=N0:N.J:LB(N0,4)=10+A*2
830 EX=N1:EV=N0
840 F.J=0TO32:IFLB(J,N0)THENEX=N0:GOS.870
850 N.J:SO.N1,N0,N0,N0:SO.3,N0,N0,N0:SO.2,100,4,EV:EV=EV-(EV>N0):IFEX THENRET.
860 G.830


870 DY=LB(J,N0):DX=LB(J,N1):LY=LB(J,2):NY=LB(J,3):NX=LB(J,4)
880 SM=SCR+40+NX+LY*160+NY*40:IF(LY+NY)THENPOKESM,N0
890 LB(J,3)=(NY+N1)-4*(INT((NY+N1)/4)):ONNY+1GOTO900,920,960,970

900 IFLY>4THENLB(J,N0)=N0:G.1030:.Scoring routine
910 POKESM+40,84:ONINT(RND(N1)*3+N1)GOTO1080,1090,1100

920 VX=N0:GOS.1020:IFSX(WY,WX)=0OR(SW(WY,WX)=SD)=0THEN940
930 VX=N1-2*SD:LB(J,N1)=VX:LB(J,3)=NY+N1:LB(J,4)=NX+VX:POKESM+40+VX,84:RET.

940 IFSW(WY,WX)=SD THENLB(J,N0)=N0:SX(WY,WX)=N1:POKESM+40,84:G.1110
950 LB(J,3)=NY+N1:POKESM+40,84:ONINT(RND(N1)*3+N1)GOTO1080,1090,1100

960 LB(J,N1)=N0:LB(J,4)=NX+DX:POKESM+40+DX,84:G.1130

970 LB(J,2)=LY+N1:POKESM+40,84:GOS.1020:SW(WY,WX)=N1-SW(WY,WX)
980 IF SX(WY,WX)=0THEN1010
990 LB(NB,N0)=N1:LB(NB,N1)=N0:LB(NB,2)=LY:LB(NB,3)=N0:LB(NB,4)=NX+2-SD*4:NB=NB+N1
1000 SX(WY,WX)=N0:POKESM-40+2-SD*4,N0:GOS.1140
1010 SX=12-WY*2+WX*4:SY=4+WY*4:WP=SW(WY,WX):GOS.760:RET.

1020 WY=LY:JX=(NX/2)+LY-6:WX=INT(JX/2):SD=JX-2*(INT(JX/2)):RET.

1030 SF=PT(RR,NX/2-N1)
1040 SG=SC(QR,RR)+SF
1050 TX=5+31*QR-(SG>9)-(SG>99)-(SG>999)
1060 TY=N1+RR:A$=STR$(SG)
1070 CX=TX:CY=TY:M$=A$:GOS.2:SC(QR,RR)=SG:G.1150

;
; Sound Effects
;
1080 SO.N1,60,10,10:RET.
1090 SO.N1,121,10,10:RET.
1100 SO.N1,81,10,10:RET.
1110 F.A=10TO30:SO.N1,A,12,10:N.A:SO.N1,N0,N0,N0:RET.
1130 F.A=40TO20STEP-N1:SO.N1,A,12,10:N.A:SO.N1,N0,N0,N0:RET.
1140 SO.2,100,4,15:EV=15:RET.
1150 SO.N1,121,10,10:SO.3,81,10,10
1160 RET.
