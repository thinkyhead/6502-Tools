1 .
2 . Self-listing Program
3 .
4 . Looks into the BASIC program buffers
5 . to print a listing of itself.
6 .

10 G.100

0 . Print Hex byte
20 IFDOHEX THENH=INT(B/16):L=B-H*16+N1:H=H+N1:? H$(H,H);H$(L,L);
21 RET.

100 CLR:POKE82,0:?CHR$(125);:SE.1,2,15
101 REA.N0,N1,N2,N3,N4,N5,N6,N7,N8,N9,DOHEX
102 D.0,1,2,3,4,5,6,7,8,9,1
103 . O.#1,8,0,"H:LISTOUT.TXT"
105 DOXP=1999:CXP=2000:BCD=2100:STR=2200:HOUT=20:MPUSH=5010:MPOP=5020:XDEC=5030
110 DIM SP$(N1),H$(16),A$(100),MS(10),E$(N1),C1$(N2),C2$(N2):SP$=" ":H$="0123456789ABCDEF":E$="":C1$=E$:C2$=SP$:IFDOHEX THENE$="|":C1$=" <":C2$="> "
115 POKE559,34:GOS.150:GOS.160:POKE559,34:G.200

150 DIMV$(100*N8):?"Vars: ";
151 S=N0:I=N1:A=PEEK(130)+256*PEEK(131)
152 IFA>=PEEK(132)+256*PEEK(133)THEN?:?:RET.
153 B=PEEK(A):A=A+N1:S=S+N1:.GOS.HOUT:?SP$;
154 O=B>=128:V$(I+S,I+S)=CHR$(B-O*128):IFNOTO THEN152
155 V$(I,I)=CHR$(S):IFI>N1 THEN?",";
156 ?V$(I+1,I+S);:I=I+N8:S=N0:G.152

160 ?"Values: ":A=PEEK(134)+256*PEEK(135)
161 IFA>=PEEK(136)+256*PEEK(137)THEN?:?:RET.
162 B=PEEK(A):A=A+N1:GOS.HOUT:I=B:GOS.195
163 ONN1+(I>=64)+(I>=128)GOSUB170,180,190:?:G.161

170 S=N6:G.196:.Scalar
180 GOS.197:GOS.197:G.197:.Array
190 GOS.197:GOS.197:G.197:.String

195 B=PEEK(A):A=A+N1:GOS.HOUT:B=B+128:?E$;:G.5040
196 F.I=1TOS:B=PEEK(A):A=A+N1:GOS.HOUT:N.I:?SP$;:RET.
197 S=N2:M=PEEK(A)+256*PEEK(A+N1):GOS.196:?"(";M;") ";:RET.

0 .
0 . Un-tokenize.
0 .
200 ST=PEEK(136)+256*PEEK(137):M=300:NS=N0:NL=N0:LO=N0:I=N0
202 B=PEEK(ST+I):.IFM<>OM THEN?:?"M=";M:?
203 GOS.HOUT:OM=M:GOS.M
204 LO=LO+N1:IFLO=NL THEN?:?:M=300:G.209
205 IFLO=NS THENM=330:IFDOHEX THEN?
209 I=I+N1:G.202

0 .
0 . Line Number, lengths
0 .
300 LO=N0:MSD=N0:LINE=B:M=310:RET.
310 LINE=LINE+256*B:IFLINE>32767THEN?:?:?"DONE!":END
315 IFDOHEX THEN? " (";LINE;") ";:G.318
316 ?LINE;":";
318 M=320:POKE82,N2:RET.
320 IFDOHEX THEN? " (NL) ";
321 NL=B:M=330:RET.
330 IFDOHEX THEN? " (NS) ";
331 ?:NS=B:M=340:RET.

0 .
0 . Handlers at 1000 + 10 * token
0 .
340 M=1000+B*10:G.400+B

0 .
0 . Command Table
0 .
400?C1$;"REM";     C2$;:RET.
401?C1$;"DATA";    C2$;:RET.
402?C1$;"INPUT";   C2$;:RET.
403?C1$;"COLOR";   C2$;:RET.
404?C1$;"LIST";    C2$;:RET.
405?C1$;"ENTER";   C2$;:RET.
406?C1$;"LET";     C2$;:RET.
407?C1$;"IF";      C2$;:RET.
408?C1$;"FOR";     C2$;:RET.
409?C1$;"NEXT";    C2$;:RET.
410?C1$;"GOTO";    C2$;:RET.
411?C1$;"GO TO";   C2$;:RET.
412?C1$;"GOSUB";   C2$;:RET.
413?C1$;"TRAP";    C2$;:RET.
414?C1$;"BYE";     C2$;:RET.
415?C1$;"CONT";    C2$;:RET.
416?C1$;"COM";     C2$;:RET.
417?C1$;"CLOSE";   C2$;:RET.
418?C1$;"CLR";     C2$;:RET.
419?C1$;"DEG";     C2$;:RET.
420?C1$;"DIM";     C2$;:RET.
421?C1$;"END";     C2$;:RET.
422?C1$;"NEW";     C2$;:RET.
423?C1$;"OPEN";    C2$;:RET.
424?C1$;"LOAD";    C2$;:RET.
425?C1$;"SAVE";    C2$;:RET.
426?C1$;"STATUS";  C2$;:RET.
427?C1$;"NOTE";    C2$;:RET.
428?C1$;"POINT";   C2$;:RET.
429?C1$;"XIO";     C2$;:RET.
430?C1$;"ON";      C2$;:RET.
431?C1$;"POKE";    C2$;:RET.
432?C1$;"PRINT";   C2$;:RET.
433?C1$;"RAD";     C2$;:RET.
434?C1$;"READ";    C2$;:RET.
435?C1$;"RESTORE"; C2$;:RET.
436?C1$;"RETURN";  C2$;:RET.
437?C1$;"RUN";     C2$;:RET.
438?C1$;"STOP";    C2$;:RET.
439?C1$;"POP";     C2$;:RET.
440?C1$;"?";       C2$;:RET.
441?C1$;"GET";     C2$;:RET.
442?C1$;"PUT";     C2$;:RET.
443?C1$;"GRAPHIC"; C2$;:RET.
444?C1$;"PLOT";    C2$;:RET.
445?C1$;"POSITION";C2$;:RET.
446?C1$;"DOS";     C2$;:RET.
447?C1$;"DRAWTO";  C2$;:RET.
448?C1$;"SETCOLOR";C2$;:RET.
449?C1$;"LOCATE";  C2$;:RET.
450?C1$;"SOUND";   C2$;:RET.
451?C1$;"LPRINT";  C2$;:RET.
452?C1$;"CSAVE";   C2$;:RET.
453?C1$;"CLOAD";   C2$;:RET.
454ONNOTDOHEX GOTO460:?C1$;"let";
455?C1$;"ERROR";   C2$;:RET.
460?C2$;:RET.

0 .
0 . REM
0 .
1000 M=1001:A$=""
1001 IFB=155THEN?SP$;CHR$(34);A$;CHR$(34);:G.1910
1002 A$(LEN(A$)+N1)=CHR$(B)
1003 .? "|î€›";CHR$(B);" ";:RET.:.REM
1004 RET.

1010 G.1000:.DATA

1020 G.DOXP:.INPUT <#SIO|VAR>
1030 G.DOXP:.COLOR <expr>
1040 G.DOXP:.LIST <expr>,<expr>
1050 G.DOXP:.ENTER <strexp>
1060 G.DOXP:.LET <var>=<expr>

1070 POKE82,PEEK(82)+N2:G.DOXP:.IF <expr> THEN <int|stmt>

1080 G.DOXP:.FOR <var>=<expr>TO<expr>STEP<expr>
1090 G.DOXP:.NEXT <var>
1100 G.DOXP:.GOTO <expr>
1110 G.DOXP:.GO TO <expr>
1120 G.DOXP:.GOSUB <expr>
1130 G.DOXP:.TRAP <expr>
1140 G.DOXP:.BYE
1150 G.DOXP:.CONT
1160 G.DOXP:.COM
1170 G.DOXP:.CLOSE
1180 G.DOXP:.CLR
1190 G.DOXP:.DEG
1200 G.DOXP:.DIM
1210 G.DOXP:.END
1220 G.DOXP:.NEW
1230 G.DOXP:.OPEN
1240 G.DOXP:.LOAD
1250 G.DOXP:.SAVE
1260 G.DOXP:.STATUS
1270 G.DOXP:.NOTE
1280 G.DOXP:.POINT
1290 G.DOXP:.XIO
1300 G.DOXP:.ON
1310 G.DOXP:.POKE
1320 G.DOXP:.PRINT
1330 G.DOXP:.RAD
1340 G.DOXP:.READ
1350 G.DOXP:.RESTORE
1360 G.DOXP:.RETURN
1370 G.DOXP:.RUN
1380 G.DOXP:.STOP
1390 G.DOXP:.POP
1400 G.DOXP:.?
1410 G.DOXP:.GET
1420 G.DOXP:.PUT
1430 G.DOXP:.GRAPHIC
1440 G.DOXP:.PLOT
1450 G.DOXP:.POSITION
1460 G.DOXP:.DOS
1470 G.DOXP:.DRAWTO
1480 G.DOXP:.SETCOLOR
1490 G.DOXP:.LOCATE
1500 G.DOXP:.SOUND
1510 G.DOXP:.LPRINT
1520 G.DOXP:.CSAVE
1530 G.DOXP:.CLOAD
1540 G.DOXP:.(let)
1550 G.1000:.ERROR

0 .
0 . End of statement
0 .
1900 IFDOHEX THEN?" (eos) :";
1901 M=330:RET.

0 .
0 . End of Line
0 .
1910 IFDOHEX THEN?" (eol) >";
1911 M=300:POKE82,N0:RET.

0 .
0 . Expr after stmt or expr
0 .
1999 MSD=N0:GOS.MPUSH:M=CXP

0 .
0 . Variable index
0 .
2000 IFB>=128THEN?E$;:G.5040:.var, ret

0 .
0 . Arguments tokens
0 .
2010 G.2000+B

2014 ONNOTDOHEX GOTOBCD:?"#";:G.BCD:.BCD
2015 G.STR:.STRING
2016 STOP:. ---
2017 STOP:. ---
2018 ?", ";:RET.:.?,
2019 STOP:. ---
2020 G.1900:. (eos) end of expr / statement
2021 ?"; ";:RET.:.?;
2022 G.1910:. (eol) end of expr / line
2023 ?"GOTO ";:RET.
2024 ?"GOSUB ";:RET.
2025 ?"TO ";:RET.
2026 ?"STEP ";:RET.
2027 ?"THEN ";:RET.
2028 ?"# ";:RET.:.OPEN #
2029 ?"<= ";:RET.:.IF A<=B
2030 ?"<> ";:RET.:.IF A<>B
2031 ?">= ";:RET.:.IF A<=B
2032 ?"< ";:RET.:.IF A<B
2033 ?"> ";:RET.:.IF A>B
2034 ?"= ";:RET.:.IF A=B
2035 ?"^ ";:RET.:.A=B^C
2036 ?"* ";:RET.:.A=B*C
2037 ?"+ ";:RET.:.A=B+C
2038 ?"- ";:RET.:.A=B-C
2039 ?"/ ";:RET.:.A=B/C
2040 ?"NOT ";:RET.
2041 ?"OR ";:RET.
2042 ?"AND ";:RET.
2043 SUB=SUB+N1:?"(m ";:RET.:.( math
2044 SUB=SUB-N1:?") ";:RET.:.exit (
2045 ?"= ";:RET.:.S1=S2
2046 ?"= ";:RET.:.N$=A$
2047 ?"<= ";:RET.:.cmp A$<=B$
2048 ?"<> ";:RET.:.cmp A$<>B$
2049 ?">= ";:RET.:.cmp A$>=B$
2050 ?"< ";:RET.:.cmp A$<B$
2051 ?"> ";:RET.:.cmp A$<B$
2052 ?"= ";:RET.:.cmp A$=B$
2053 ?"+ ";:RET.:.POSITIVE
2054 ?"- ";:RET.:.NEGATIVE
2055 SUB=SUB+1:?"(g ";:RET.:.A$( G.MPUSH
2056 SUB=SUB+1:ONNOTDOHEX GOTO2099:?"(a ";:RET.:.SC(
2057 SUB=SUB+1:?"(s ";:RET.:.DIM SC(
2058 SUB=SUB+1:?"(c ";:RET.:.CHR$(, PEEK(
2059 SUB=SUB+1:?"(d ";:RET.:.DIM S$(
2060 ?", ";:RET.
2061 ?"STR$ ";:RET.
2062 ?"CHR$ ";:RET.
2063 ?"USR ";:RET.
2064 ?"ASC ";:RET.
2065 ?"VAL ";:RET.
2066 ?"LEN ";:RET.
2067 ?"ADR ";:RET.
2068 ?"ATN ";:RET.
2069 ?"COS ";:RET.
2070 ?"PEEK ";:RET.
2071 ?"SIN ";:RET.
2072 ?"RND ";:RET.
2073 ?"FRE ";:RET.
2074 ?"EXP ";:RET.
2075 ?"LOG ";:RET.
2076 ?"CLOG ";:RET.
2077 ?"SQR ";:RET.
2078 ?"SGN ";:RET.
2079 ?"ABS ";:RET.
2080 ?"INT ";:RET.
2081 ?"PADDLE ";:RET.
2082 ?"STICK ";:RET.
2083 ?"PTRIG ";:RET.
2084 ?"STRIG ";:RET.
2099 RET.

0 . Scalar
2100 GOS.MPUSH:M=2101:SC=N6:SV=N0:RET.
2101 XP=N0:IFB THENXP=100^(B-64-N4)
2102 M=2103:G.2104
2103 GOS.XDEC:SV=SV*100+V
2104 SC=SC-N1:IFSC>N0 THENRET.
2105 ?E$;(SV*XP);SP$;:G.MPOP

0 . = after LET var
2110 IFB=14THENSC=N6:?"=";:M=DOXP:RET.

0 . SUBSCRIPT
0 . 2120 GOS.MPUSH:M=2121:RET.
0 . 2121 IFB=37THEN?", ";
0 . 2122 RET.

0 . String
2200 GOS.MPUSH:M=2202:SC=N0:A$="":RET.
2202 IFSC=N0 THENSC=B:IFSC=N0 THEN2210
2204 IFDOHEX THEN?CHR$(34);SP$;
2205 M=2206:RET.
2206 A$(LEN(A$)+N1)=CHR$(B)
2208 SC=SC-N1:IFSC>N0 THENRET.
2210 ?SP$;CHR$(34);A$;CHR$(34);SP$;:G.MPOP

0 . MPUSH
5010 MS(MSD)=M:MSD=MSD+N1:RET.

0 . MPOP
5020 IFNOTMSD THEN?"<No Back>":RET.
5021 MSD=MSD-N1:M=MS(MSD):RET.:.?"Back to ";M

0 . bcd_to_dec
5030 H=INT(B/16):V=B-H*16+H*10:RET.

0 . var_name(B)
5040 H=B*N8-1023:L=ASC(V$(H)):?V$(H+N1,H+L);SP$;:RET.

0 DIMZ(5):Z(0)=1:Z(1)=2:Z(2)=3:Z(3)=4:Z(4)=5

