0 .
0 . BDOS.LST
0 . A bare-bones CLI using file funcs
0 . by Thinkyhead
0 .

1 POK.82,0:POK.709,15:POK.710,0:?:? "BDOS - (c) 2019 Thinkyhead"

2 DIMT$(80),C$(80),F$(80),ARG$(78),HIS$(650),CD$(3),CMD$(10)
3 DIMSRC$(20),DST$(20),CO$(14),M$(1),K$(1),EOL$(1),YN$(2),SP$(40)

4 HP=0:CD$="D:":CO$="Couldn't open ":EOL$=CHR$(155):YN$="NY":SP$=" ":SP$(40)=SP$:SP$(2)=SP$(1)
5 DIMINV$(26):INV$="code":REM InvertString(adr,len)
6 O.#2,4,0,"K:"
7 T.8:F$=CD$:GOS.1360:O.#1,6,0,F$:CL.#1:G.10
8 T.9:F$="H:":GOS.1360:O.#1,6,0,F$:CD$="H:":CL.#1:G.10
9 ?CO$;" any drive. Warning!"
10 ?:? CD$;">";:GOS.5000:ONNOTL GOTO10:NOARG=L<LEN(CMD$)+2
100 ERR=0:T.9999:GOS.7100:T$=C$:GOS.5901
101 IFCMD$="?"THEN? EOL$;"Commands: ? CD CP CLS LS MV Q RM":G.10
110 IFCMD$="LS"THENGOS.1100:G.10
120 IFCMD$="CP"THENGOS.1200:G.10
130 IFCMD$="CD"THENGOS.1300:G.10
135 T$=CMD$:GOS.6200:IFI=LEN(CMD$)THENT$=CMD$:GOS.1350:G.10
140 IFCMD$="CLS"THENGOS.1400:G.10
141 IFCMD$="DOS"THENDOS
150 IFCMD$="RM"THENGOS.1500:G.10
160 IFCMD$="MV"THENGOS.1600:G.10
170 IFCMD$="CAT"THENGOS.1700:G.10
180 IFCMD$="Q"THENEND
0 .
0 . Run a BASIC program?
0 .
190 IFL<5THEN199
191 IFC$(L-3)<>".BAS"THEN199
192 F$=CD$:F$(LEN(F$)+1)=C$
193 ?"Run ";F$;"? ";:GOS.8000:IFNOTYN THEN10
194 T.195:RUN F$
195 ?"Can't run ";F$:G.10

0 .
0 . Command Error
0 .
199 ERR=1
0 .
0 . Go back to prompt
0 .
200 ONERR GOSUB9998:G.10

1100 .
1101 .LS -L <PATT>
1102 .
1105 LL=0:C=LEN(C$):IFNOARG THENF$=CD$:G.1120

1110 IFLEN(ARG$)>1THENLL=ARG$(1,2)="-L":IFLL THENL=L-3
1111 T=4+LL*3:IFC>=T THENT$=C$(T):GOS.6200:IFI THENF$=C$(T):G.1120
1112 F$=CD$:IFC>=T THENF$(LEN(F$)+1)=C$(T)

1120 GOS.1360:T.1125:O.#1,6,0,F$:T.1124:COUNT=0
1121 F.X=0 TO 63:I.#1;F$:IFNOTLEN(F$)THEN1124
1122 V=F$(1,2)="* ":T=V OR F$(1,2)="  ":IFT THENF$=F$(3):COUNT=COUNT+1
1123 ONLL+1GOSUB1130,1140:N.X
1124 CL.#1:RET.
1125 ST.#1,T:?"Error - ";T:G.1124

0 . Staggered output
1130 E=X-3*INT(X/3):ONNOTT GOTO1135:F$=F$(1,11):GOSUB 6400
1131 L=LEN(F$):IFV THENV=USR(INV,ADR(F$),L)
1132 ? F$;:IFE<2THEN?SP$(1,14-L);:RET. 
1133 IFL<12THEN?
1134 RET. 
0 . Print last line straight
1135 IFE THEN?
0 . Print any line straight
1140 IFNOTT ANDNS THENRET.
1141 IFNOTT THEN?
1142 IFV THENV=USR(INV,ADR(F$),LEN(F$))
1143 ? F$:RET.

1200 .
1201 . CP TRACER H:
1202 .
1210 IFNOARG THEN1220
1211 T$=C$(4):GOS.5901
1212 E=L*(NOTI)+(I+2)*(I<>0):SRC$=C$(4,E):IFNOTI OR L<=I THEN1221
1214 DST$=C$(I+4):G.1240

1220 ?"Src";:I.SRC$
1221 ?"Dst";:I.DST$

0 . Got base SRC,DST
0 . Put a drive prefix on both
1240 GOS.5800:T$=DST$:GOS.5900:IFI=LEN(DST$)THEN GOS.1280:G.1245
1241 IFNOTI THENDST$=CD$:DST$(LEN(DST$)+1)=T$
1242

0 . Full SRC,DST. Begin copy
1245 ?"Copy ";SRC$;" to ";DST$;"? ";:GOS.8000:IFNOTYN THENRET.

1250 T.1290:O.#1,4,0,SRC$:T.1291:O.#3,8,0,DST$
1251 T.1252:?"Please wait...";:F.I=1TO999999:GET#1,C:PUT#3,C:N.I
1252 ?"DONE":?
1253 CL.#1:CL.#3:RET.

0 . Append src file onto DST if needed
1280 Q=I:T$=SRC$:GOS.5900:DST$(Q+1)=SRC$((I>1)*I+1):RET.

1290 ? CO$;"src ";SRC$:RET.
1291 ? CO$;"dst ";DST$:RET.


1300 .
1301 . CD
1302 .
1305 IFL<4THEN?"CWD is ";CD$:RET.
1306 T$=C$(4):GOS.6200:IFNOTI THEN199
1307 T$=C$(4,3+I):.GOS.1350:RET.


1350 T.1353:F$=T$:GOS.1360:O.#1,6,0,F$:CD$=T$
1351 T.1352:CL.#1
1352 RET.
1353 ST.#1,T:?"Error - ";T:G.1351

1360 T=LEN(F$):IFF$(T)=":"THENF$(T+1)="*.*"
1361 RET.

1400 .
1401 . CLS
1402 .
1403 ?CHR$(125):RET.


1500 .
1501 . RM
1502 .
1510 IFNOARG THEN1520
1511 T$=C$(4):GOS.5901
1512 E=L*(NOTI)+(I+2)*(I<>0):SRC$=C$(4,E):G.1540

1520 ?"Filespec";:I.SRC$:IFSRC$=""THENRET.

1540 GOS.5800
1541 T=0:FORI=1TOLEN(SRC$):T=T+SRC$(I,I)="*":N.I:IFNOTT THEN1545
1542 ?"Files matching ";SRC$;"...":C$(1,2)="LS":NS=1:GOS.1100:NS=0
1543 IFCOUNT THEN?"Delete all? ";:G.1546
1544 ?"No matches":RET.
1545 ?"Delete ";SRC$;"? ";
1546 GOS.8000:IFNOTYN THENRET.

1550 T.1560:XIO 33,#3,0,0,SRC$:?"Deleted":RET.
1560 ST.#3,T:?"Error - ";T:RET.

1600 .
1601 . MV
1602 .
1603 ?"MV Coming Soon!":RET.


1700 .
1701 . CAT
1702 .
1710 IFNOARG THEN1720
1711 T$=C$(5):GOS.5901
1712 E=L*(NOTI)+(I+2)*(I<>0):SRC$=C$(5,E):G.1740

1720 ?"Filespec";:I.SRC$:IFSRC$=""THENRET.

1740 GOS.5800:SCR=PEEK(88)+256*PEEK(89)
1741 ?"Contents of ";SRC$
1750 T.1760:O.#3,4,0,SRC$:?
1751 T.1761:F.I=0TO999999:GET#3,C:IFC=155THENC=27+256
1752 ?CHR$(27);CHR$(C);:IFC>255THENPOKESCR+PEEK(84)*40+PEEK(85)-1,219
1753 ONPEEK(764)<>255GOTO1762:N.I
1760 ST.#3,T:?"Error - ";T;
1761 ?:CL.#3:RET.
1762 GET #2,K:?:?"<stopped>":G.1761

5000 .
5001 .Get a command
5002 .
5003 C$="":CMD$=""
5004 L=LEN(C$):GET #2,K:K$=CHR$(K)
5005 IFK=28 OR K=29THENGOS.7000:G.5004
5006 IFK<32 OR K=127THEN5004
5007 IFK=126THENGOS.5040:G.5004
5008 IFK<>32THEN5020
5009 IFNOTL THEN5004
5010 IFC$(L)=" "THEN5004

0 . Print char, commit on enter
5020 ?K$;:IFK=155THEN5030

0 . Add the char, loop
5021 C$(L+1)=CHR$(K):G.5004

0 . Set command and return
5030 IFNOTL THENRET.

5031 IFC$(L)=" "THENL=L-1:C$=C$(1,L):G.5031
5032 T$=C$:GOS.5901:IFNOTI THENCMD$=C$:ARG$="":RET.
5033 CMD$=C$(1,I-1):ARG$=C$(I+1):RET.

5040 IFL=0THENRET.
5041 ?K$;:IFL=1THENC$="":RET.
5042 C$=C$(1,L-1):RET.

0 . Apply CD$ drive prefix to SRC$
5800 T$=SRC$:GOS.5900:IFNOTI THENSRC$=CD$:SRC$(LEN(SRC$)+1)=T$
5801 RET.

0 . Find colons or spaces
5900 M$=":":G.6000
5901 M$=" ":G.6000
0 . I=strpos(M$ in T$)
6000 M=LEN(M$):T=LEN(T$):IFNOTT ORM>T THEN6003
6001 F.I=1TOT-M+1:IFT$(I,I+M-1)=M$THENRET.
6002 N.I
6003 I=0:RET.

0 . M$=GetVolume (T$)
6100 GOS.6200:M$="":IFI THENM$=T$(1,I)
6101 RET.

0 . PrefixLen - returned in I
0 . Also: ValidPrefix test
6200 I=0:Q=LEN(T$):IFQ<2THENRET.
6201 M$=T$(1,1):IFM$<>"D" AND M$<>"H"THENRET.
6202 M$=T$(2,2):IFM$=":"THENI=2:RET.
6203 IFQ>2ANDM$>="1"ANDM$<="8"ANDT$(3,3)=":"THENI=3
6204 RET.

0 . ValidFilename(T$)
6300 GOS.5900:IFI THENGOS.6200:IFNOTI THENRET. :.invalid prefix?
0 . Check for proper naming
6301 M$=".":GOS.6000:IFI>8THEN6320. :.filename too long
6302 IFNOTI ANDLEN(T$)>8THENRET.
6302 IFI ANDL>I+3THEN6320           :.ext too long
6310 I=LEN(T$):RET.
6320 I=0:RET.

0 . F$=CompactName(F$)
6400 T$=F$(1,8):GOS.6420:ARG$=T$
6401 T$=F$(9):GOS.6420:F$=ARG$:I=LEN(F$):IFT$<>""THENF$(I+1)=".":F$(I+2)=T$
6402 RET.

6420 I=LEN(T$):IFT$(I,I)<>" "THENRET.
6421 IFI=1THENT$="":RET.
6422 T$=T$(1,I-1):G.6420

0 . History Buffer
7000 IFLEN(HIS$)=0THEN RET.

0 . Move up and down
7001 OP=HP:HP=HP+(K=28)-(K=29):IFHP<0THENHP=0:RET.
7002 IFHP=0THENC$="":G.7011
7003 S1=LEN(HIS$)+2:C=HP
7004 F.J=S1-2 TO 1 STEP -1:IFHIS$(J,J)<>EOL$ THEN7008
7005 S2=S1-2:S1=J+1:C=C-1:IFNOTC THEN7010
7008 N.J:IFC>0THENHP=HP-1:RET.
7009 S2=S1:S1=1
7010 C$=HIS$(S1,S2)
7011 IFL THENT=LEN(CD$)+PEEK(82)+1:M=PEEK(84):POS.T,M:?SP$(1,L);:POS.T,M
7012 ?C$;:L=S2-S1+1:RET.

7100 .Save a command
7101 IFNOTL THENRET.
7102 T=LEN(HIS$)+1:IFT+L>200THENT$=HIS$:M$=EOL$:GOS.6000:HIS$=HIS$(I+1):G.7102
7103 HIS$(T)=EOL$:HIS$(T+1)=C$:HP=0
7104 RET.

8000 GET#2,K:K$=CHR$(K):YN=K$="Y":?YN$(YN+1,YN+1):RET.

8888 S."D:BDOS.BAS":?"SAVED!":END

9998 ?"   ";C$;"?? (";CMD$;")":ERR=0:RET.
9999 ? "ERROR":G.10

0
RUN
